plugins {
	id 'com.github.johnrengelman.shadow' version '2.0.1'
	id 'com.palantir.git-version' version '0.8.0'
	id 'java'
}

repositories {
	mavenCentral()
	maven { url 'https://libraries.minecraft.net/' }
}

dependencies {
	compile 'org.ow2.asm:asm:5.2'
	compile 'org.yaml:snakeyaml:1.18'
	compileOnly ('net.minecraft:launchwrapper:1.12') {
		transitive = false
	}
	testCompile 'junit:junit:4.12'
}

sourceCompatibility = 1.8
version = '1.0.' + (System.getenv('BUILD_NUMBER')?:'0-SNAPSHOT') + '-' + versionDetails().gitHash[0..6]

jar {
	manifest {
		attributes (
			'Implementation-Title': project.name,
			'Implementation-Version': version,
			'Premain-Class': 'org.to2mbn.authlibinjector.javaagent.AuthlibInjectorPremain',
			'TweakClass': 'org.to2mbn.authlibinjector.tweaker.AuthlibInjectorTweaker'
		)
	}
}

shadowJar {
	classifier = null

	exclude 'META-INF/maven/**'

	relocate 'org.yaml.snakeyaml', 'org.to2mbn.authlibinjector.internal.org.yaml.snakeyaml'
	relocate 'org.objectweb.asm', 'org.to2mbn.authlibinjector.internal.org.objectweb.asm'
}
